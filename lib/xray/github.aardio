//github 代理设置
import xray.core;
import process.popen;
namespace xray.github;

setProxy = function(enableProxy){
	var socksPort = ..xray.core.socksProxyPort;
	var httpPort = ..xray.core.httpProxyPort;
	if((!socksPort || !httpPort) && enableProxy) return;

	var git;
	if(enableProxy){
		git = ..process.popen("git config --global http.https://github.com.proxy http://127.0.0.1:" + httpPort)
		if (!git){
			return
		}

		var path = ..io.getSpecial(0x28/*_CSIDL_PROFILE*/,".ssh/config")
		var str = ..string.load(path):"";
		if(..string.find(str,"Host\s+github.com" ) ){
			if(!..string.find(str,"Host\s+github.com\s+ProxyCommand\s+connect\s+\-S\s+127\.0\.0\.1\:\d+ \-a none \%h \%p" )){
				str = ..string.replace(str,"Host\s+github.com",'Host github.com\r\n    ProxyCommand connect -S 127.0.0.1:' + socksPort + ' -a none %h %p')
			}
		}
		else {
			if (str==""){
				str = 'Host github.com\r\n    ProxyCommand connect -S 127.0.0.1:' + socksPort + ' -a none %h %p'
			} else {
				if (!..string.endWith(str, '\r\n', true)){
					str = str + '\r\n'
				}
				str = str + '\r\nHost github.com\r\n    ProxyCommand connect -S 127.0.0.1:' + socksPort + ' -a none %h %p'			
			}
		}

		..string.save(path,str)
	}
	else {
		git = ..process.popen("git config --global --unset http.https://github.com.proxy")
		if (!git){
			return
		}

		var path = ..io.getSpecial(0x28/*_CSIDL_PROFILE*/,".ssh/config")
		var str = ..string.load(path):"";
		if(..string.find(str,"Host\s+github.com")){
			if(..string.find(str,"Host\s+github.com\s+ProxyCommand\s+connect\s+\-S\s+127\.0\.0\.1\:\d+ \-a none \%h \%p" )){
				str = ..string.replace(str,"\s*Host\s+github.com\s+ProxyCommand\s+connect\s+\-S\s+127\.0\.0\.1\:\d+ \-a none \%h \%p",'')
				..string.save(path,str)
			}
		}
	}
}